buildscript {
	repositories {
        jcenter()
    }
    final def androidPluginVersion = "2.3.3"
    final def dokkaPluginVersion = "0.9.15"
    dependencies {
        classpath "com.android.tools.build:gradle:$androidPluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$props_kotlinversion"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokkaPluginVersion"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion Integer.parseInt(props_compilesdkversion)
    buildToolsVersion props_buildtoolsversion
    defaultConfig {
        applicationId props_applicationidbase
        versionCode Integer.parseInt(props_versioncode)
        versionName props_versionname
        minSdkVersion props_minsdkversion
        targetSdkVersion props_targetsdkversion
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        preDexLibraries false
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$project.buildDir/lint/report.html")
        quiet false
        warningsAsErrors true
    }
    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }
    variantFilter {
    	if (it.buildType.name == 'release') {
    		it.setIgnore(true)
    	}
    }
}
repositories {
    jcenter()
    google()
}
final def espressoVersion = "3.0.1"
final def junitVersion = "4.12"
final def retrofitVersion = "2.3.0"
final def supportLibraryVersion = "26.0.2"
dependencies {
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$props_kotlinversion"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$espressoVersion") {
        exclude group: "com.android.support"
    }
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-moshi:$retrofitVersion"
    testCompile "junit:junit:$junitVersion"
}
